2 PRINT "DEPTH CHARGE"
4 PRINT "CREATIVE COMPUTING MORRISON, NEW JERSY"
6 PRINT:PRINT:PRINT
10 PRINT "DEPTH CHARGE GAME":PRINT
20 INPUT "DIMENSION OF THE SEARCH AREA";G:PRINT
30 N=INT(LOG(G)/LOG(2))+1
40 PRINT "YOU ARE THE CAPTAIN OF THE DESTROYER USS COMPUTER"
100 PRINT "GOOD LUCK!":PRINT
110 A=RND(G):B=RND(G):C=RND(G)
120 FOR D=1 TO N:PRINT:PRINT "TRIAL #";D;"ENTER X,Y,Z";:INPUT X,Y,Z
130 IF ABS(X-A)+ABS(Y-B)+ABS(Z-C)=0 THEN 300
140 GOSUB 500 : PRINT : NEXT D
200 PRINT : PRINT "YOU HAVE BEEN TORPEDOED!"
210 PRINT "THE SUBMARINE WAS AT";A;",";B;",";C : GOTO 400
300 PRINT : PRINT "B O O M ! !   YOU FOUND IT IN";D;"TRIES!"
400 PRINT : PRINT : INPUT "ANOTHER GAME (Y OR N)";A$
410 IF A$="Y" THEN 100
420 PRINT "OK. THANKS FOR PLAYING" : GOTO 600
500 PRINT "SONAR REPORTS SHOT WAS ";
510 IF Y>B THEN PRINT "NORTH";
520 IF Y<B THEN PRINT "SOUTH";
530 IF X>A THEN PRINT "EAST";
540 IF X<A THEN PRINT "WEST";
545 PRINT " OF THE SUB";
550 IF Y<>B OR X<>A THEN PRINT " AND";
560 IF Z>C THEN PRINT " TOO LOW."
570 IF Z<C THEN PRINT " TOO HIGH."
580 IF Z=C THEN PRINT " DEPTH OK."
590 RETURN 
600 END
