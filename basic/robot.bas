5 POKE &HFFD9,0
10 REM ROBOT MINEFIELD
20 GOTO 250
30 REM MOVE ROBOTS
40 TALLY = 0
50 FOR E=1 TO 4
60 IF A(B(E),C(E))=42 THEN TALLY=TALLY+1 : GOTO 200
70 X=B(E) : Y=C(E)
80 IF B(E)<D THEN B(E)=B(E)+1
90 IF B(E)>D AND RND(10)>3 THEN B(E)=B(E)-1
100 IF C(E)<F AND RND(10)>3 THEN C(E)=C(E)+1
110 IF C(E)>F THEN C(E)=C(E)-1
120 IF B(E)<2 THEN B(E)=2
130 IF B(E)>14 THEN B(E)=14
140 IF C(E)<2 THEN C(E)=2
150 IF C(E)>14 THEN C(E)=14
160 A(X,Y)=46
170 IF A(B(E),C(E))=42 THEN TALLY=TALLY+1 : SOUND 1,1 : GOTO 200
180 IF A(B(E),C(E))=72 THEN A(B(E),C(E))=36:GOSUB 300:GOTO 910
190 A(B(E),C(E))=36
200 NEXT E

210 IF TALLY<CH THEN TALLY=CH
220 CH=TALLY
230 IF TALLY=4 THEN GOSUB 300:GOTO 970
240 RETURN
250 GOSUB 530:REM INIT
260 GOSUB 300:REM PRINT MINEFIELD
270 GOSUB 30:REM ROBOT MOVE
280 GOSUB 420:REM HUMAN MOVE
290 GOTO 260

300 REM PRINT MINEFIELD
310 PRINT @0, "DEAD ROBOT TALLY:";TALLY;
350 FOR B=1 TO 15
360  FOR C=1 TO 15
370   PRINT @B*32+(C-1)*2,CHR$(A(B,C));
380  NEXT C
400 NEXT B
410 RETURN 

420 REM PLAYER MOVE
430 A(D,F)=46
440 A$=INKEY$
450 IF A$="" THEN 440
460 IF A$="N" AND D>2 THEN D=D-1
470 IF A$="S" AND D<14 THEN D=D+1
480 IF A$="E" AND F<14 THEN F=F+1
490 IF A$="W" AND F>2 THEN F=F-1
500 IF A(D,F)=42 THEN GOSUB 300:GOTO 990
510 A(D,F)=72
520 RETURN 

530 REM INIT
550 DIM A(15,15),B(4),C(4)
560 CLS
570 PRINT:PRINT"PLEASE STAND BY..."
580 CH=0

610 REM PLACE WALLS
620 FOR B=1 TO 15
630  FOR C=1 TO 15
640   A(B,C)=46
650   IF B=1 OR B=15 OR C=1 OR C=15 THEN A(B,C)=88
660  NEXT C
665 NEXT B

670 REM PLACE MINES
680 FOR B=1 TO 20
690  C=RND(13)
700  D=RND(13)
710  IF A(C,D)=88 THEN 690
720  A(C,D)=42
730 NEXT B
740 DATA 4,4,13,8,8,12,7

750 REM PLACE ROBOTS
760 FOR E=1 TO 4
770  D=RND(13)+2
780  F=RND(13)+2
790  IF A(D,F) <> 46 THEN 770
800  B(E) = D:C(E)=F
810  A(B(E),C(E))=36
820 NEXT E
830 REM PLACE HUMAN
840 D=RND(13)+2
850 F=RND(13)+2
860 IF A(D,F)<>46 THEN 830
870 A(D,F)=72
875 CLS
880 RETURN

890 REM HUMAN AT D,F
900 REM ROBOTS AT B(E),C(E)
910 REM END OF GAME
920 CLS:PRINT "A ROBOT HAS GOT YOU!!"
930 CH=0:TALLY=0:GOSUB 340
940 A$=""
950 IF INKEY$<>"" THEN 950
960 GOSUB 570:GOTO 260
970 CLS:PRINT "YOU'VE DEFEATED THE ROBOTS!!"
980 GOTO 930
990 CLS:PRINT "YOU'VE RUN INTO A MINE!!"
1000 GOTO 930
